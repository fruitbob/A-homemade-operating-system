; haribote-os boot asm
; TAB=4
[INSTRSET "i486p"]				; 486gLq
BOTPAK	EQU		0x00280000		; kernel[h
DSKCAC	EQU		0x00100000		; fBXNLbV
DSKCAC0	EQU		0x00008000		; fBXNLbViA[hj

; BOOT_INFOW
CYLS	EQU		0x0ff0			; u[gZN^
LEDS	EQU		0x0ff1
VMODE	EQU		0x0ff2			; FBrbgJ[H
SCRNX	EQU		0x0ff4			; xX
SCRNY	EQU		0x0ff6			; xY
VRAM	EQU		0x0ff8			; OtBbNobt@Jnn
VBEMODE	EQU		0x105           ;1024*768*8bit
		ORG		0xc200			; vO
;设置显卡模式

		MOV		AX,0x9000       ;测试是否可以使用vbe
		MOV		ES,AX
		MOV		DI,0
		MOV		AX,0x4f00
		INT		0x10
		CMP		AX,0x004f
		JNE		scrn320     ;不能测试用原始的320*200

;vbe可用，则测试vbe版本，2.0以下仍不能使用高分辨率

		MOV		AX,[ES:DI+4]
		CMP		AX,0x0200
		JB		scrn320			; if (AX < 0x0200) goto scrn320

;测试是否可以使用指定的分辨率

		MOV		CX,VBEMODE
		MOV		AX,0x4f01
		INT		0x10
		CMP		AX,0x004f
		JNE		scrn320

;测试模式是否可用

		CMP		BYTE [ES:DI+0x19],8     ;颜色数是否为8，不是不能用
		JNE		scrn320
		CMP		BYTE [ES:DI+0x1b],4      ;指定颜色，不为4不能用
		JNE		scrn320
		MOV		AX,[ES:DI+0x00]          ;测试模式属性第7bit是否为1，为1表示可以使用
		AND		AX,0x0080                ;与运算，结果为0说明7bit为0
		JZ		scrn320			; 若7bit为0，则不能用

; 通过测试，保存图像信息

		MOV		BX,VBEMODE+0x4000
		MOV		AX,0x4f02
		INT		0x10
		MOV		BYTE [VMODE],8	; [hiCQj
		MOV		AX,[ES:DI+0x12]
		MOV		[SCRNX],AX
		MOV		AX,[ES:DI+0x14]
		MOV		[SCRNY],AX
		MOV		EAX,[ES:DI+0x28]
		MOV		[VRAM],EAX
		JMP		keystatus

scrn320:
		MOV		AL,0x13			; VGAOtBbNXA320x200x8bitJ[
		MOV		AH,0x00
		INT		0x10
		MOV		BYTE [VMODE],8	; [hiCQj
		MOV		WORD [SCRNX],320
		MOV		WORD [SCRNY],200
		MOV		DWORD [VRAM],0x000a0000


; L[{[hLEDBIOS
keystatus:
		MOV		AH,0x02
		INT		0x16 			; keyboard BIOS
		MOV		[LEDS],AL

; PICt
;	AT@dlAPICA
;	CLIOAnOAbv
;	PIC

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; OUTA@
		OUT		0xa1,AL

		CLI						; CPUx~

; CPU1MBANZXAA20GATE

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL
		CALL	waitkbdout

; veNg[hs



		LGDT	[GDTR0]			; bGDT
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit310iy[WO~j
		OR		EAX,0x00000001	; bit01iveNg[hsj
		MOV		CR0,EAX
		JMP		pipelineflush
pipelineflush:
		MOV		AX,1*8			;  \ZOg32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; kernel]

		MOV		ESI,kernel	; ]
		MOV		EDI,BOTPAK		; ]
		MOV		ECX,512*1024/4
		CALL	memcpy

; fBXNf[^{u]

; u[gZN^

		MOV		ESI,0x7c00		; ]
		MOV		EDI,DSKCAC		; ]
		MOV		ECX,512/4
		CALL	memcpy

; cS

		MOV		ESI,DSKCAC0+512	; ]
		MOV		EDI,DSKCAC+512	; ]
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; V_oCg/4
		SUB		ECX,512/4		; IPL
		CALL	memcpy

; asmheadSIA
;	kernelC

; kernelN

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; ]
		MOV		ESI,[EBX+20]	; ]
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; ]
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; X^bNl
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; AND0waitkbdout
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; Z0memcpy
		RET
; memcpyAhXTCYvtBNXYAXgO

		ALIGNB	16
GDT0:
		RESB	8				; kZN^
		DW		0xffff,0x0000,0x9200,0x00cf	; \ZOg32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; s\ZOg32bitikernelpj

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
kernel:
